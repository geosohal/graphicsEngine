// vertical blur pass

#version 430 // Version of OpenGL with COMPUTE shader support

layout(local_size_x = 1, local_size_y = 148, local_size_z = 1) in;
// Declares thread group size

uniform blurKernel {float weights [101]; }; // Declares a uniform block

uniform int kernalSize = 3; // size must always be odd number to have a whole number half width

layout (r32f) uniform readonly image2D src; // src image as single channel 32bit float readonly
layout (r32f) uniform writeonly image2D dst; // ds

shared float v[148+101]; // Variable shared with other threads in the 1x148 thread group

void main()
{
    int w = (kernalSize - 1) / 2;

    //vertical pass, since the gpos offsets affect y component

    ivec2 gpos = ivec2(gl_GlobalInvocationID.xy); // Combo of groupID, groupSize and localID
    uint i = gl_LocalInvocationID.y; // Local thread id in the 1x148 thread groups
    v[i] = imageLoad(src, gpos+ivec2(0,-w) ).x; // read an image pixel at an ivec2(.,.) position
    if (i<2*w)
         v[i+148] = imageLoad(src, gpos+ivec2(0,148-w) ).x; // read extra 2*w pixels. .x needed??
    barrier(); // Wait for all threads to catchup before reading v[]

    // for each thread and its corresponding pixel in thread group, multiply weights and pixels
    float blurredPixel = 0.f;
    for (int j = 0; j <= 2*w; j++)    // remember, this is loop for single thread
    {
        uint vertIndex = (i+j);
        blurredPixel += weights[j] * v[vertIndex]; // or i+j??
    }

    imageStore(dst, gpos, vec4(blurredPixel)); // Write to destination image
}
